#!/usr/bin/python3

"""Build for spowtd docs

Build requires:
 - asciidoc
 - pdflatex
 - scons

"""

ENV = Environment()

ENV.Command(target='doc/spowtd.1',
            source='src/spowtd.1.txt',
            action='a2x -f manpage -D doc $SOURCE')

ENV.Command(target='build/doc/rise_event_error.pdf',
            source='src/rise_event_error.svg',
            action='inkscape $SOURCE -o $TARGET -C')
ENV.Command(target=['build/doc/user_guide.aux', 'build/doc/user_guide.fls'],
            source=['src/user_guide.tex', 'build/doc/rise_event_error.pdf'],
            action=('pdflatex -halt-on-error --output-directory=build/doc '
                    '-recorder '
                    '${SOURCES[0]}'))
ENV.Command(target='build/doc/user_guide.bib',
            source='src/user_guide.bib',
            action='cp -a $SOURCE $TARGET')
ENV.Command(target='build/doc/user_guide.bbl',
            source=['build/doc/user_guide.aux',
                    'build/doc/user_guide.bib'],
            action='bibtex user_guide',
            chdir=True)
ENV.Command(target='build/doc/user_guide.pdf',
            source='build/doc/user_guide.fls',
            action=('for i in {1..5}; do '
                    'pdflatex --output-directory=build/doc -halt-on-error '
                    'src/user_guide.tex'
                    '; done'))
ENV.Depends('build/doc/user_guide.pdf', 'build/doc/user_guide.bbl')

ENV.Command(target='doc/user_guide.pdf',
            source='build/doc/user_guide.pdf',
            action='cp -a $SOURCE $TARGET')
